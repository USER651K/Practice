*&---------------------------------------------------------------------*
*& Include          ZFIVENDCD_SCR
*&---------------------------------------------------------------------*

FORM fetch_and_process_data.

  DATA: lv_orig_blart       TYPE bkpf-blart,
        lv_vendor_name      TYPE lfa1-name1,
        lv_mandi            TYPE zp2pttruck-zp2pdemandi,
        lv_payment_reason   TYPE bseg-rstgr,
        lv_days             TYPE i,
        lv_payment_due_date TYPE bsik-zfbdt,
        lv_zuonr_10         TYPE char10.

  " Cell style variables - ADDED
  DATA: lt_cellstyle TYPE lvc_t_styl,
        ls_cellstyle TYPE lvc_s_styl.

  " Clear previous data
  CLEAR: gt_credit, lt_bsik_temp, lt_lfa1, lt_mandi, lt_bseg, lt_bkpf.

  " SELECT FROM BSIK (Main Data)
  SELECT bukrs lifnr gjahr belnr buzei budat bldat gsber waers
         wrbtr skfbt zfbdt rebzg rebzj rebzz shkzg blart zterm
         zbd1t zbd2t zbd3t zbd1p zbd2p augdt zuonr
    FROM bsik
    INTO CORRESPONDING FIELDS OF TABLE lt_bsik_temp
    WHERE bukrs IN s_bukrs
      AND lifnr IN s_zp2pde
      AND budat IN s_budat
      AND gsber IN s_destpl
      AND shkzg = 'H'
      AND blart IN ('RE', 'KR', 'AB').

  IF lt_bsik_temp[] IS NOT INITIAL.

    " Fetch BKPF data
    SELECT bukrs belnr gjahr blart
      FROM bkpf
      INTO CORRESPONDING FIELDS OF TABLE lt_bkpf
      FOR ALL ENTRIES IN lt_bsik_temp
      WHERE bukrs = lt_bsik_temp-bukrs
        AND belnr = lt_bsik_temp-rebzg
        AND gjahr = lt_bsik_temp-rebzj.

    SORT lt_bkpf BY bukrs belnr gjahr.

    " Fetch LFA1 data
    SELECT lifnr name1
      FROM lfa1
      INTO CORRESPONDING FIELDS OF TABLE lt_lfa1
      FOR ALL ENTRIES IN lt_bsik_temp
      WHERE lifnr = lt_bsik_temp-lifnr.

    SORT lt_lfa1 BY lifnr.

    " Fetch Mandi data
    SELECT zp2pdetrno zp2pdemandi
      FROM zp2pttruck
      INTO CORRESPONDING FIELDS OF TABLE lt_mandi
      FOR ALL ENTRIES IN lt_bsik_temp
      WHERE zp2pdetrno = lt_bsik_temp-zuonr(10).

    SORT lt_mandi BY zp2pdetrno.

    " Fetch BSEG data
    SELECT bukrs belnr gjahr buzei rstgr koart
      FROM bseg
      INTO CORRESPONDING FIELDS OF TABLE lt_bseg
      FOR ALL ENTRIES IN lt_bsik_temp
      WHERE bukrs = lt_bsik_temp-bukrs
        AND belnr = lt_bsik_temp-belnr
        AND gjahr = lt_bsik_temp-gjahr
        AND buzei = lt_bsik_temp-buzei
        AND koart = 'K'.

    SORT lt_bseg BY bukrs belnr gjahr buzei.

    " Process data
    LOOP AT lt_bsik_temp INTO ls_bsik_temp.

      CLEAR: lv_orig_blart, lv_vendor_name, lv_mandi, lv_payment_reason,
             lv_days, lv_payment_due_date, lv_zuonr_10, gs_credit,
             lt_cellstyle, ls_cellstyle.

      " READ BKPF (for AB documents)
      IF ls_bsik_temp-blart = 'AB'.
        READ TABLE lt_bkpf INTO ls_bkpf
          WITH KEY bukrs = ls_bsik_temp-bukrs
                   belnr = ls_bsik_temp-rebzg
                   gjahr = ls_bsik_temp-rebzj
          BINARY SEARCH.

        IF sy-subrc = 0.
          lv_orig_blart = ls_bkpf-blart.
        ENDIF.

        IF lv_orig_blart <> 'RE' AND lv_orig_blart <> 'KR'.
          CONTINUE.
        ENDIF.
      ENDIF.

      " READ LFA1
      READ TABLE lt_lfa1 INTO ls_lfa1
        WITH KEY lifnr = ls_bsik_temp-lifnr
        BINARY SEARCH.

      IF sy-subrc = 0.
        lv_vendor_name = ls_lfa1-name1.
      ENDIF.

      " READ MANDI
      lv_zuonr_10 = ls_bsik_temp-zuonr(10).
      READ TABLE lt_mandi INTO ls_mandi
        WITH KEY zp2pdetrno = lv_zuonr_10
        BINARY SEARCH.

      IF sy-subrc = 0.
        lv_mandi = ls_mandi-zp2pdemandi.
      ELSE.
        CONTINUE.
      ENDIF.

      " FILTER by Mandi
      IF s_zp2p IS NOT INITIAL.
        CHECK lv_mandi IN s_zp2p.
      ENDIF.

      " READ BSEG
      READ TABLE lt_bseg INTO ls_bseg_data
        WITH KEY bukrs = ls_bsik_temp-bukrs
                 belnr = ls_bsik_temp-belnr
                 gjahr = ls_bsik_temp-gjahr
                 buzei = ls_bsik_temp-buzei
        BINARY SEARCH.

      IF sy-subrc = 0.
        lv_payment_reason = ls_bseg_data-rstgr.
      ENDIF.

      " Calculate Payment Due Date
      IF ls_bsik_temp-zbd3t IS NOT INITIAL.
        lv_payment_due_date = ls_bsik_temp-zfbdt + ls_bsik_temp-zbd3t.
      ELSEIF ls_bsik_temp-zbd2t IS NOT INITIAL.
        lv_payment_due_date = ls_bsik_temp-zfbdt + ls_bsik_temp-zbd2t.
      ELSEIF ls_bsik_temp-zbd1t IS NOT INITIAL.
        lv_payment_due_date = ls_bsik_temp-zfbdt + ls_bsik_temp-zbd1t.
      ELSE.
        lv_payment_due_date = ls_bsik_temp-zfbdt.
      ENDIF.

      " Calculate days
      lv_days = sy-datum - ls_bsik_temp-zfbdt.

      " Populate output structure
      gs_credit-checkmark        = ''.
      gs_credit-manual_edit      = ''.
      gs_credit-payment_reason   = lv_payment_reason.
      gs_credit-payment_term     = ls_bsik_temp-zterm.
      gs_credit-payment_amt      = ls_bsik_temp-wrbtr.
      gs_credit-cd_level1_days   = ls_bsik_temp-zbd1t.
      gs_credit-cd_level1_pct    = ls_bsik_temp-zbd1p.
      gs_credit-cd_level2_days   = ls_bsik_temp-zbd2t.
      gs_credit-cd_level2_pct    = ls_bsik_temp-zbd2p.
      gs_credit-zfbdt            = ls_bsik_temp-zfbdt.
      gs_credit-payment_due_date = lv_payment_due_date.
      gs_credit-payment_date     = ls_bsik_temp-augdt.
      gs_credit-zfbdt_days       = lv_days.

      " Calculate Applicable CD
      PERFORM calculate_applicable_cd USING ls_bsik_temp-zfbdt
                                            ls_bsik_temp-zbd1t
                                            ls_bsik_temp-zbd2t
                                            ls_bsik_temp-zbd1p
                                            ls_bsik_temp-zbd2p
                                            ls_bsik_temp-wrbtr
                                            lv_payment_due_date
                                   CHANGING gs_credit-applicable_cd_pct
                                            gs_credit-applicable_cd_amt.

      gs_credit-rebzg         = ls_bsik_temp-rebzg.
      gs_credit-rebzj         = ls_bsik_temp-rebzj.
      gs_credit-rebzz         = ls_bsik_temp-rebzz.
      gs_credit-debit_note_no = ls_bsik_temp-rebzg.
      gs_credit-bukrs         = ls_bsik_temp-bukrs.
      gs_credit-zp2pdemandi   = lv_mandi.
      gs_credit-lifnr         = ls_bsik_temp-lifnr.
      gs_credit-vendor_name   = lv_vendor_name.
      gs_credit-gjahr         = ls_bsik_temp-gjahr.
      gs_credit-belnr         = ls_bsik_temp-belnr.
      gs_credit-buzei         = ls_bsik_temp-buzei.
      gs_credit-budat         = ls_bsik_temp-budat.
      gs_credit-bldat         = ls_bsik_temp-bldat.
      gs_credit-gsber         = ls_bsik_temp-gsber.
      gs_credit-waers         = ls_bsik_temp-waers.
      gs_credit-wrbtr         = ls_bsik_temp-wrbtr.
      gs_credit-skfbt         = ls_bsik_temp-skfbt.

      ">>> ADDED: Initialize cell styles for PAYMENT_AMT field
      " By default, PAYMENT_AMT is disabled (manual_edit unchecked)
      ls_cellstyle-fieldname = 'PAYMENT_AMT'.
      ls_cellstyle-style     = cl_gui_alv_grid=>mc_style_disabled.
      APPEND ls_cellstyle TO lt_cellstyle.
      gs_credit-celltab = lt_cellstyle.
      "<<< END ADDED

      APPEND gs_credit TO gt_credit.

    ENDLOOP.

    " Set flag for successful data fetch
    IF gt_credit IS NOT INITIAL.
      gv_data_fetched = 'X'.
    ELSE.
      CLEAR gv_data_fetched.
      MESSAGE 'No data to display after filters' TYPE 'S'.
    ENDIF.

  ELSE.
    CLEAR gv_data_fetched.
    MESSAGE 'No data found for selection criteria' TYPE 'S'.
  ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*& Form calculate_applicable_cd
*&---------------------------------------------------------------------*
FORM calculate_applicable_cd USING p_zfbdt    TYPE bsik-zfbdt
                                   p_zbd1t    TYPE bsik-zbd1t
                                   p_zbd2t    TYPE bsik-zbd2t
                                   p_zbd1p    TYPE bsik-zbd1p
                                   p_zbd2p    TYPE bsik-zbd2p
                                   p_amount   TYPE bsik-wrbtr
                                   p_due_date TYPE bsik-zfbdt
                          CHANGING p_cd_pct   TYPE p
                                   p_cd_amt   TYPE bsik-wrbtr.

  DATA: lv_days           TYPE i,
        lv_cd_level1_date TYPE sy-datum,
        lv_cd_level2_date TYPE sy-datum.

  lv_days = sy-datum - p_zfbdt.
  lv_cd_level1_date = p_zfbdt + p_zbd1t.
  lv_cd_level2_date = p_zfbdt + p_zbd2t.

  IF sy-datum <= lv_cd_level1_date AND p_zbd1p IS NOT INITIAL.
    p_cd_pct = p_zbd1p.
  ELSEIF sy-datum > lv_cd_level1_date AND
         sy-datum <= lv_cd_level2_date AND
         p_zbd2p IS NOT INITIAL.
    p_cd_pct = p_zbd2p.
  ELSE.
    p_cd_pct = 0.
  ENDIF.

  IF p_cd_pct > 0.
    p_cd_amt = ( p_amount * p_cd_pct ) / 100.
  ELSE.
    p_cd_amt = 0.
  ENDIF.

ENDFORM.















FORM post_selected_documents.

  DATA: lv_tabix      TYPE sy-tabix,
        lv_posted_doc TYPE belnr_d,
        lv_message    TYPE char100,
        lv_success    TYPE char1,
        lt_celltab    TYPE lvc_t_styl,
        ls_celltab    TYPE lvc_s_styl.

  FIELD-SYMBOLS: <fs_credit> TYPE ty_credit.

  CLEAR: gv_post_count, gv_error_count.

  LOOP AT gt_credit ASSIGNING <fs_credit>.

    " Skip if not selected or already posted
    IF <fs_credit>-checkmark <> 'X' OR <fs_credit>-posted = 'X'.
      CONTINUE.
    ENDIF.

    " Validate: Payment amount must be > 0
    IF <fs_credit>-payment_amt <= 0.
      <fs_credit>-post_message = 'No Payment amount to post'.
      gv_error_count = gv_error_count + 1.
      CONTINUE.
    ENDIF.

    " Call BAPI to post document
    CLEAR: lv_posted_doc, lv_message, lv_success.
    PERFORM call_bapi_acc_document_post
      USING    <fs_credit>
      CHANGING lv_posted_doc
               lv_message
               lv_success.

    IF lv_success = 'X'.
      " Update row directly via field-symbol
      <fs_credit>-posted       = 'X'.
      <fs_credit>-post_doc_no  = lv_posted_doc.
      <fs_credit>-post_message = lv_message.
      <fs_credit>-checkmark    = ' '.

      " Update cell style to disable checkmark
      CLEAR lt_celltab.
      CLEAR ls_celltab.
      ls_celltab-fieldname = 'CHECKMARK'.
      ls_celltab-style     = cl_gui_alv_grid=>mc_style_disabled.
      APPEND ls_celltab TO lt_celltab.
      <fs_credit>-celltab = lt_celltab.

      " Update custom table
      PERFORM update_vendcd_doc USING <fs_credit>.

      gv_post_count = gv_post_count + 1.
    ELSE.
      <fs_credit>-post_message = lv_message.
      gv_error_count = gv_error_count + 1.
    ENDIF.

  ENDLOOP.

ENDFORM.
